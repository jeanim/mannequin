global proc chartreuseContextProperties() {
  setUITemplate -pushTemplate DefaultTemplate;

  string $label = `toolPropertyWindow -q -field`;
  text -e -label "Chartreuse Settings" $label;

  string $parent = `toolPropertyWindow -q -location`;
  setParent $parent;

  if (`columnLayout -ex chartreuseSettingsLayout`) {
      deleteUI chartreuseSettingsLayout;
  }

  columnLayout -adjustableColumn true chartreuseSettingsLayout;
    separator -height 5 -style "none";
    button -label "Show Chartreuse Palette" -command "chartreusePaletteBegin";
    separator -height 10 -style "none";

  setUITemplate -popTemplate;
  chartreusePaletteBegin;
}

global proc chartreuseContextValues(string $name) {
  toolPropertySelect chartreuseSettingsLayout;
}

global proc chartreuseContextFinish() {
  chartreusePaletteFinish;
}

global proc chartreusePaletteBegin() {
  if (!`dockControl -ex chartreusePaletteDock`) {
    window chartreusePaletteWindow;
    menuBarLayout;
      textField chartreuseSearchField;
      separator -height 5 -style "none";
    setParent ..;
    scrollLayout -childResizable true;
      columnLayout -adjustableColumn true chartreusePaletteLayout;
      setParent ..;
    setParent ..;

    dockControl -area "right"
                -content chartreusePaletteWindow
                -label "Chartreuse"
                -allowedArea "left"
                -allowedArea "right" chartreusePaletteDock;

    // The rest of the palette UI is done in Python because it uses Qt/PySide.
    python "setupChartreuseUI()";
  }

  evalDeferred "dockControl -e -visible true -raise chartreusePaletteDock";
}

global proc chartreusePaletteFinish() {
  python "tearDownChartreuseUI()";
  if (`dockControl -ex chartreusePaletteDock`) {
    evalDeferred "deleteUI chartreusePaletteDock";
  }
}

global proc chartreuseInstallShelf() {
  if (`about -batch`) {
    return;
  }

  if (!`shelfLayout -ex Chartreuse`) {
    evalDeferred "loadNewShelf \"shelf_Chartreuse.mel\"";
  }
}

global proc chartreuseTool() {
  if (!`chartreuseContext -ex globalChartreuseContext`) {
    chartreuseContext globalChartreuseContext;
  }

  setToolTo globalChartreuseContext;
}
